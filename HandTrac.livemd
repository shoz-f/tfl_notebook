# Hand Tracking by DeepLab

## 1.Helper module

```elixir
defmodule Model do
  @model_file "hand_trac.tflite"

  @wearhouse "https://github.com/shoz-f/tinyML_livebook/releases/download/model/#{@model_file}"
  @local "/data/#{@model_file}"

  def file() do
    @local
  end

  def get() do
    Req.get!(@wearhouse).body
    |> then(fn x -> File.write(@local, x) end)
  end

  def rm() do
    File.rm(@local)
  end

  def exists?() do
    File.exists?(@local)
  end
end
```

```elixir
Model.get()
```

## 2.Inference Module

```elixir
defmodule HandTrac do
  # TflInterp, model: Model.file()
  use TflInterp

  @handtrack_shape {300, 300}
  @threshold 0.9

  alias CImg.Builder

  def apply(jpeg) do
    # preprocess
    bin =
      CImg.from_binary(jpeg)
      |> CImg.resize(@handtrack_shape)
      |> CImg.to_binary(range: {-1.0, 1.0})

    # prediction
    __MODULE__
    |> TflInterp.set_input_tensor(0, bin)
    |> TflInterp.invoke()

    [bboxes, scores] =
      for i <- [0, 2] do
        TflInterp.get_output_tensor(__MODULE__, i)
        |> Nx.from_binary({:f, 32})
        |> Nx.reshape({10, :auto})
      end

    # postprocess
    index =
      Nx.to_flat_list(scores)
      |> Enum.with_index()
      |> (&(for {score, index} <- &1, score >= @threshold do
              index
            end)).()

    unless index == [] do
      bboxes
      |> Nx.take(Nx.tensor(index))
      |> Nx.to_batched_list(1)
    else
      []
    end
  end

  def draw_result(results, jpeg) do
    Enum.reduce(results, Builder.from_binary(jpeg), fn box, canvas ->
      [y1, x1, y2, x2] = Nx.to_flat_list(box)
      CImg.draw_rect(canvas, x1, y1, x2, y2, {255, 0, 0})
    end)
    |> Builder.runit()
    |> CImg.resize({640, 480})
    |> CImg.to_binary(:jpeg)
  end
end
```

```elixir
HandTrac.start_link(model: Model.file())
```

```elixir
TflInterp.info(HandTrac)
```

## 3.Demo

```elixir
Kino.animate(10, 0, fn i ->
img = Picam.next_frame()

  res =
    HandTrac.apply(img)
    |> HandTrac.draw_result(img)
    |> Kino.Image.new(:jpeg)

  {:cont, res, i + 1}
  #:halt
end)
```
